BCD、独热码、
### 二进制转BCD（0709）
bcd：用4位二进制表示一位十进制

**转换方法：**

（1）对10求余（除10取余）
        8位二进制转12位bcd（255）
        百位bcd[11:8] = bin/100    （如果不确定最高位是百位，用： (bin/100)%10）
        十位bcd[7:4] = (bin/10)%10
        个位bcd[3:0] = bin%10;
        
        缺点：占用的逻辑资源较多

（2）大四加三移位法
![[Pasted image 20230201165722.png|500]]
`方法流程：8位二进制转12位bcd，首先bin前拼12位的寄存器，初始为全0，然后把bin的数值进行左移每次只能移1位，然后对12位寄存器中每4位的数值做判断，如果大于4则加3，否则保持不变，加完之后继续移位，直到bin全部移出，此时12位寄存器的数值就是最终的bcd码。`

**顶层**
![[Pasted image 20230201170311.png|300]]

**内部结构**
![[Pasted image 20230201170412.png|200]]

![[Pasted image 20230201170510.png|600]]

代码

```verilog
//除10取余
module bin_to_bcd (bin, bcd);
	
	input [7:0] bin;
	output reg [11:0] bcd;
	
	always @ (*)
		begin
			bcd[11:8] = bin / 100;
			bcd[7:4] = (bin / 10) % 10;
			bcd[3:0] = bin % 10;
		end 

endmodule 
```

```verilog
module bin_to_bcd_top #(
								parameter BIN_WIDTH = 8,
								parameter BCD_WIDTH = 12)(bin, bcd);
	
	input [BIN_WIDTH-1:0] bin;
	
	output [BCD_WIDTH-1:0] bcd;
	
//	wire [19:0] temp [8:0];
//	
//	assign temp[0] = {12'd0, bin};
//	
//	move m1(.data_in(temp[0]), .data_out(temp[1]));
//	move m2(.data_in(temp[1]), .data_out(temp[2]));
//	move m3(.data_in(temp[2]), .data_out(temp[3]));
//	move m4(.data_in(temp[3]), .data_out(temp[4]));
//	move m5(.data_in(temp[4]), .data_out(temp[5]));
//	move m6(.data_in(temp[5]), .data_out(temp[6]));
//	move m7(.data_in(temp[6]), .data_out(temp[7]));
//	move m8(.data_in(temp[7]), .data_out(temp[8]));
//
//	assign bcd = temp[8][19:8];

	wire [BIN_WIDTH+BCD_WIDTH-1:0] temp [BIN_WIDTH:0];
	
	assign temp[0] = {{BCD_WIDTH{1'b0}}, bin};
	
	genvar i;
	
	generate
		for(i = 0; i < BIN_WIDTH; i = i + 1)
			begin	:	move_inst
				move #(.BIN_WIDTH(BIN_WIDTH),.BCD_WIDTH(BCD_WIDTH))
						m1 (.data_in(temp[i]), .data_out(temp[i+1]));
			end 
	endgenerate
	
	assign bcd = temp[BIN_WIDTH][BIN_WIDTH+BCD_WIDTH-1:BIN_WIDTH];

endmodule 
```

```verilog
module move #(
					parameter BIN_WIDTH = 8,
					parameter BCD_WIDTH = 12)(data_in, data_out);
	
	input [BIN_WIDTH + BCD_WIDTH - 1:0] data_in;
	
	output [BIN_WIDTH + BCD_WIDTH - 1:0] data_out;
	
//	wire [19:0] data_reg;
//	wire [3:0] d_reg1, d_reg2, d_reg3;   //wire [3:0] d_reg [2:0]
//	
//	adjust a1(
//					.d_in(data_in[19:16]),
//					.d_out(d_reg1)
//				);
//	
//	adjust a2(
//					.d_in(data_in[15:12]),
//					.d_out(d_reg2)
//				);
//	
//	adjust a3(
//					.d_in(data_in[11:8]),
//					.d_out(d_reg3)
//				);
//	
//	assign data_reg = {d_reg1, d_reg2, d_reg3, data_in[7:0]};
//	assign data_out = data_reg << 1;

	wire [BIN_WIDTH + BCD_WIDTH - 1:0] data_reg;
	
	genvar i;
	
	generate 
		for(i = 0; i < BCD_WIDTH/4; i = i + 1)
			begin	:	adjust_inst
				adjust a1(.d_in(data_in[BIN_WIDTH+BCD_WIDTH-1-i*4:BIN_WIDTH+BCD_WIDTH-4-i*4]), 
							.d_out(data_reg[BIN_WIDTH+BCD_WIDTH-1-i*4:BIN_WIDTH+BCD_WIDTH-4-i*4]));
			end 
	endgenerate
	
	assign data_reg[BIN_WIDTH-1:0] = data_in[BIN_WIDTH-1:0];
	assign data_out = data_reg << 1;
	
endmodule 
```

```verilog
module adjust (d_in, d_out);
	
	input [3:0] d_in;
	
	output [3:0] d_out;
	
	assign d_out = (d_in > 4) ? d_in + 3 : d_in;
	
endmodule 
```

```verilog
`timescale 1ns/1ps

module bin_to_bcd_tb;

	wire [7:0] bin;
	
	wire [11:0] bcd;
	
	wire [11:0] tb_bcd;
	
	reg [7:0] mem [9:0];
	reg [3:0] mem_addr;
	
	bin_to_bcd_top dut(
							.bin(bin), 
							.bcd(bcd)
						);
						
	assign tb_bcd[11:8] = bin / 100;
	assign tb_bcd[7:4]  = (bin / 10) % 10;
	assign tb_bcd[3:0] = bin % 10;
	
//	initial begin 
//		bin = 0;
//		
//		repeat(20)
//			begin
//				bin = {$random} % 256;
//				#20;
//			end 
//		#200 $stop;
//	end 
	
	always @ (*)					//打印到报告
		begin
			if(bcd == tb_bcd)
				$display("true, bin = %d, bcd = %h, tb_bcd = %h",bin, bcd, tb_bcd);
			else
				$display("error, bin = %d, bcd = %h, tb_bcd = %h",bin, bcd, tb_bcd);
		end 
		
	integer out_file;
	
	initial begin
		out_file = $fopen("data_out.txt");  //.dat  .hex  .mif
	end 
	
	always @ (*)					//打印到文件
		begin
			if(bcd == tb_bcd)
				$fdisplay(out_file, "true, bin = %d, bcd = %h, tb_bcd = %h",bin, bcd, tb_bcd);
			else
				$fdisplay(out_file, "error, bin = %d, bcd = %h, tb_bcd = %h",bin, bcd, tb_bcd);
		end
	
	initial begin
		$readmemb("E:/20220625/bin_to_bcd/sim/data.txt", mem);
			mem_addr = 0;
			repeat(10)
				begin
					#20 
					mem_addr = mem_addr + 1;
				end 
				
		#20 $stop;
	end 
	
	assign bin = mem[mem_addr];
	
endmodule 
```